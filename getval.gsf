***************************************************************************************
*	$Id: getval.gsf,v 1.6 2012/05/16 23:28:30 bguan Exp $
*	Copyright (C) 2011 Bin Guan.
*	Distributed under GNU/GPL.
***************************************************************************************
function getval(expr,fmt)
*
* Return value of a GrADS expression in designated format.
*
* Note 1: By default, GrADS only displays 6 significant digits (%.6g) when using 'display ...' or 'query defval ...'.
*         A trick is used below to surpass that limitation (up to 15 significant digits can be returned).
* Note 2: 'display ...' or 'set gxout print' is not used here for getting variable values since that will trigger a bug that prevents the base map from being displayed in contour plots.
*
* Example: sst=getval('sst','%.1f')
*          Note that sst is quoted.
*
rc=gsfallow('on')

qdims()

'set x '_xs
'set y '_ys
'set z '_zs
'set t '_ts

'nonexistentvar='expr
'query defval nonexistentvar 1 1'
part1=subwrd(result,3)
'nonexistentvar='expr'-'part1
'query defval nonexistentvar 1 1'
part2=subwrd(result,3)
'nonexistentvar='expr'-'part1'-'part2
'query defval nonexistentvar 1 1'
part3=subwrd(result,3)
'undefine nonexistentvar'

'set x '_xs' '_xe
'set y '_ys' '_ye
'set z '_zs' '_ze
'set t '_ts' '_te

return math_format(fmt,part1+part2+part3)
